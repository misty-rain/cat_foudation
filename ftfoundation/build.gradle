apply plugin: 'maven'
apply plugin: 'com.android.library'
apply plugin: 'signing'


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.friendtimes.sdk:ft_eventbus:1.0.1'
    compile 'com.friendtimes.sdk:ft_logger:1.0.0'
    compile 'com.friendtimes.sdk:ft_json:1.0.1'
    compile 'com.friendtimes.sdk:ft_http:1.1.2'


}

task jarMyLib(type: Jar, dependsOn: ['build']) {
    archiveName = 'ftfoundation_1.1.5.jar'
    from('build/intermediates/classes/release')
    destinationDir = file('build/libs')
    exclude('com/friendtime/foundation/BuildConfig.class')
    exclude('com/friendtime/foundation/BuildConfig\$*.class')
    exclude('**/R.class')
    exclude('**/R\$*.class')
    exclude('android/**')
    include('com/friendtime/foundation/**')
}


def isReleaseBuild() {
    return FT_FOUNDATION_VERSION_NAME.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL :
            'http://192.168.0.177:8081/nexus/content/repositories/releases'
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL :
            'http://192.168.0.177:8081/nexus/content/repositories/snapshots'
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : 'admin'
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : 'admin123'
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = FT_FOUNDATION_POM_GROUP_ID
                pom.artifactId = FT_FOUNDATION_POM_ARTIFACT_ID
                pom.version = FT_FOUNDATION_VERSION_NAME
                pom.packaging = FT_FOUNDATION_POM_PACKAGING

                repository(url: getReleaseRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name FT_FOUNDATION_POM_NAME
                    description FT_FOUNDATION_POM_DESCRIPTION

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            name POM_DEVELOPER_NAME
                            id POM_DEVELOPER_ID
                            email POM_DEVELOPER_EMAIL
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task sourceCode(type: Zip) {
        appendix = extension = 'src'
        from 'AndroidManifest.xml'
        into('res') {
            from 'res'
        }
        into('src') {
            from 'src'
        }
        into('libs') {
            from 'libs'
        }
    }

    task androidJavadocs(type: Javadoc) {
        options.encoding = "utf-8"
        source = android.sourceSets.main.java.srcDirs
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath += files(ext.androidJar)
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives sourceCode
        archives androidSourcesJar
       // archives androidJavadocsJar
    }
}
